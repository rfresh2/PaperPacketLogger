plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
}

group = "vc.packetlogger"
version = "1.0"

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    maven {
        name 'papermc'
        url 'https://repo.papermc.io/repository/maven-public/'
        content {
            includeModule("io.papermc.paper", "paper-api")
            includeModule("io.papermc", "paperlib")
            includeModule("net.md-5", "bungeecord-chat")
        }
    }
    maven {
        name 'jitpack'
        url 'https://jitpack.io'
    }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
    maven { url 'https://repo.codemc.io/repository/maven-releases/' }
    mavenCentral()
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT'
    implementation 'io.papermc:paperlib:1.0.8'
//    implementation("com.github.retrooper.packetevents:spigot:2.0-SNAPSHOT")
    compileOnly group: "com.comphenix.protocol", name: "ProtocolLib", version: "5.1.0";
    compileOnly 'com.github.retrooper.packetevents:spigot:2.0.2'
    testImplementation 'io.papermc.paper:paper-api:1.20.2-R0.1-SNAPSHOT'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
}

test {
    useJUnitPlatform()
}

processResources {
    filesMatching("**/plugin.yml") {
        expand ( NAME: rootProject.name, VERSION: version, PACKAGE: project.group.toString() )
    }
}

shadowJar {
    archiveClassifier.set('')
    relocate 'io.papermc.lib', 'shadow.io.papermc.paperlib'
//    relocate 'com.github.retrooper.packetevents', 'vc.com.github.retrooper.packetevents'
//    relocate 'io.github.retrooper.packetevents', 'vc.io.github.retrooper.packetevents'
//    minimize()
}

// Disable jar and replace with shadowJar
jar.enabled = false
assemble.dependsOn(shadowJar)

tasks.register('printProjectName') {
    doLast {
        println rootProject.name
    }
}

tasks.register('release') {
    dependsOn build

    doLast {
        if (!version.endsWith("-SNAPSHOT")) {
            // Rename final JAR to trim off version information
            shadowJar.archiveFile.get().getAsFile()
                    .renameTo(layout.buildDirectory.get().toString() + File.separator + 'libs' + File.separator
                            + rootProject.name + '.jar')
        }
    }
}
